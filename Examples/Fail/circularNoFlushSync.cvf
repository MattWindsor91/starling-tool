/*
 * Circular buffer with an erroneous lack of synchrononisation when
 * flushing.
 */

// The circular buffer itself.
shared int[100] circ_buf;

// Capacities.
shared int r_capacity;
shared int w_capacity;

// Local state: the reader and writer both have an inner pointer and a
// local buffer.
thread int      pointer;
thread int[100] local_buf;

method write(int c)
{
  {| Writer(pointer, 0) * local{0 <= c <= 100} |}
    thread int wc; <| wc = w_capacity; |>
  {| Writer(pointer, wc) * local{0 <= c <= 100} |}
    if (wc < c) {
      {| Writer(pointer, wc) * local{0 <= c <= 100} |}
        c = wc;
      {| Writer(pointer, wc) * local{0 <= c <= wc} |}
    }
  {| Writer(pointer, wc) * local{0 <= c <= wc} |}
    thread int wrote;
    wrote = 0;
  {| Writer(pointer, wc) * local{0 <= wrote <= c <= wc} |}
    while (wrote < c) {
      {| Writer(pointer, wc) * local{0 <= wrote < c <= wc} |}
        <| circ_buf[pointer] = local_buf[wrote]; |>
        pointer = (pointer + 1) % 100;
        wrote++;
      {| Writer(pointer, wc) * local{0 < wrote <= c <= wc} |}
    }
  {| Writer(pointer, wc) * local{0 <= wrote <= wc} |}
    <| w_capacity = w_capacity - wrote;
       r_capacity = r_capacity + wrote; |>
  {| Writer(pointer, wc - wrote) |}
}

method read(int c)
{
  {| Reader(pointer, 0) * local{0 <= c <= 100} |}
    thread int rc;
    <| rc = r_capacity; |>
  {| Reader(pointer, rc) * local{0 <= c <= 100} |}
    if (rc < c) {
      {| Reader(pointer, rc) * local{0 <= c <= 100} |}
        c = rc;
      {| Reader(pointer, rc) * local{0 <= c <= rc} |}
    }
  {| Reader(pointer, rc) * local{0 <= c <= rc} |}
    thread int read;
    read = 0;
  {| Reader(pointer, rc) * local{0 <= read <= c <= rc} |}
    while (read < c) {
      {| Reader(pointer, rc) * local{0 <= read < c <= rc} |}
        <| local_buf[read] = circ_buf[pointer]; |>
        pointer = (pointer + 1) % 100;
        read++;
      {| Reader(pointer, rc) * local{0 < read <= c <= rc} |}
    }
  {| Reader(pointer, rc) * local{0 <= read <= rc} |}
    <| r_capacity = r_capacity - read;
       w_capacity = w_capacity + read; |>
  {| Reader(pointer, rc - read) |}
}

// Erroneous flush
method flush()
{
  {| emp |}
    <| r_capacity = 0;
       w_capacity = 100; |>
  {| emp |}
}
// End erroneous flush

/* The above specifications require the reader and writer to have a capacity
   estimate of 0, so we show here that we can always forget the last capacity
   estimate we had. */

method forget_rcap(int c)
{
  {| Reader(pointer, c) |} ; {| Reader(pointer, 0) |}
}

method forget_wcap(int c)
{
  {| Writer(pointer, c) |} ; {| Writer(pointer, 0) |}
}

constraint emp ->
    0 <= w_capacity &&
    0 <= r_capacity &&
    w_capacity + r_capacity == 100;

view Writer(int pointer, int cap_estimate);
constraint Writer(pointer, cap_estimate) ->
  0 <= pointer < 100 && 0 <= cap_estimate <= w_capacity;
// Single writer
constraint Writer(xp, xc) * Writer(yp, yc) -> false;

view Reader(int pointer, int cap_estimate);
constraint Reader(pointer, cap_estimate) ->
  0 <= pointer < 100 && 0 <= cap_estimate <= r_capacity;
// Single reader
constraint Reader(xp, xc) * Reader(yp, yc) -> false;
