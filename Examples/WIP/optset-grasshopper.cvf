pragma grasshopper_include {./grasshopper/optset-module.spl};
pragma grasshopper_footprint {X};
pragma grasshopper_footprint_sort {Set<Entry>}; // X

typedef int Entry;

shared Entry head, tail;
shared int ub, lb;

thread bool present, done;
thread int ckey;
thread Entry pred, curr;

view isHead(Entry h), isTail(Entry t), isEntry(Entry t), isEntryOrTail(Entry t);
view keyIs(int k, Entry e), isMarked(Entry e), isExterior(Entry e);
view wf(int key);
view less(Entry x, Entry y);

view hasLock(Entry p), lockedAndValidated(Entry p, Entry q);

view removed(int k);

method remove(int key) {
    {| wf(key) |}
        do {
            {| wf(key) |}
                <pred = head>;
            {| wf(key) * isHead(pred) |}
                curr = %{ #1.next }(pred);
            {| wf(key) * isHead(pred)
               * isEntryOrTail(curr)  // Set could be empty -> curr==tail.
            |}
                ckey = %{ #1.key }(curr);
            {| wf(key) * isHead(pred) * isEntryOrTail(curr)
               * keyIs(ckey, curr)  // We just read it, and it never changes.
            |}
                while (ckey < key) {
                    {| wf(key)
                       * isEntry(curr)  // If this was tail, ckey > key.
                       // Throw everything else away because we're
                       // re-establising it below.
                    |}
                        pred = curr;
                    {| wf(key) * isEntry(pred) * isEntry(curr) |}
                        curr = %{ #1.next }(pred);
                    {| wf(key) * isEntry(pred)
                       * isEntryOrTail(curr) // See above.
                    |}
                        ckey = %{ #1.key }(curr);
                    {| wf(key) * isEntry(pred) * isEntryOrTail(curr)
                       * keyIs(ckey, curr)  // See above.
                    |}
                }
            {| wf(key) * isEntry(pred) * isEntryOrTail(curr) * keyIs(ckey, curr) |}
                <%{ waitLock(#1); takeLock(#1) }(pred)[]>;
            {| wf(key) * hasLock(pred) * isEntryOrTail(curr) * keyIs(ckey, curr) |}
                <%{ waitLock(#1); takeLock(#1) }(curr)[]>;
            {| wf(key) * hasLock(pred) * hasLock(curr) * keyIs(ckey, curr) |}
                if (%{ validate(#1, #2) == true }(pred, curr)) {
                    {| wf(key) * lockedAndValidated(pred, curr) * keyIs(ckey, curr) |}
                        if (ckey != key) {
                            {| wf(key) * lockedAndValidated(pred, curr) |}
                                present = false;
                                done = true;
                            {| wf(key) * hasLock(pred) * hasLock(curr) * if (present == true) { removed(key) } else { emp } |}
                        } else {
                            {| wf(key)
                               * isEntry(curr)  // from wf(key) and ckey == key
                               * lockedAndValidated(pred, curr)
                               * keyIs(key, curr)
                            |}
                                <%{ #1.marked := true }(curr)[]>;
                            {| wf(key) * isEntry(curr) * lockedAndValidated(pred, curr)
                               * isMarked(curr)  // From the previous action
                               * removed(key)    // From the previous action
                            |}
                                <%{ #1.next := #2.next }(pred, curr)[]>;
                            {| wf(key) * hasLock(pred) * hasLock(curr) * removed(key)
                               // We've used less now so we no longer need it.
                            |}
                                present = true;
                                done = true;
                            {| wf(key) * hasLock(pred) * hasLock(curr) * if (present == true) { removed(key) } else { emp } |}
                        }
                    {| wf(key) * hasLock(pred) * hasLock(curr) * if (present == true) { removed(key) } else { emp } |}
                } else {
                    {| wf(key) * hasLock(pred) * hasLock(curr) |}
                        // We shouldn't need to do this...?
                        present = false;
                        done = false;
                    {| wf(key) * hasLock(pred) * hasLock(curr) * if (present == true) { removed(key) } else { emp } |}
                }
            {| wf(key) * hasLock(pred) * hasLock(curr) * if (present == true) { removed(key) } else { emp } |}
                <%{ releaseLock(#1) }(curr)[]>;
            {| wf(key) * hasLock(pred) * if (present == true) { removed(key) } else { emp } |}
                <%{ releaseLock(#1) }(pred)[]>;
            {| wf(key) * if (present == true) { removed(key) } else { emp } |}
        } while (done == false);
    {| if (present == true) { removed(key) } else { emp } |}
}

method add(Entry e, int key) {
    {| fresh(e, key) |}
        // This proceeds exactly as remove, until we validate.
        do {
            {| fresh(e, key) |}
                <pred = head>;
            {| fresh(e, key) * isHead(pred) |}
                curr = %{ #1.next }(pred);
            {| fresh(e, key) * isHead(pred) * isEntryOrTail(curr) |}
                ckey = %{ #1.key }(curr);
            {| fresh(e, key) * isHead(pred) * isEntryOrTail(curr)
               * keyIs(ckey, curr)
            |}
                while (ckey < key) {
                    {| fresh(e, key) * isEntry(curr) |}
                        pred = curr;
                    {| fresh(e, key) * isEntry(pred) * isEntry(curr) |}
                        curr = %{ #1.next }(pred);
                    {| fresh(e, key) * isEntry(pred)
                       * isEntryOrTail(curr) // See above.
                    |}
                        ckey = %{ #1.key }(curr);
                    {| fresh(e, key) * isEntry(pred) * isEntryOrTail(curr)
                       * keyIs(ckey, curr)  // See above.
                    |}
                }
            {| fresh(e, key) * isEntry(pred) * isEntryOrTail(curr) * keyIs(ckey, curr) |}
                <%{ waitLock(#1); takeLock(#1) }(pred)[]>;
            {| fresh(e, key) * hasLock(pred) * isEntryOrTail(curr) * keyIs(ckey, curr) |}
                <%{ waitLock(#1); takeLock(#1) }(curr)[]>;
            {| fresh(e, key) * hasLock(pred) * hasLock(curr) * keyIs(ckey, curr) |}
                if (%{ validate(#1, #2) == true }(pred, curr)) {
                    {| fresh(e, key) * lockedAndValidated(pred, curr) * keyIs(ckey, curr) |}
                        if (ckey == key) {
                            {| fresh(e, key) * lockedAndValidated(pred, curr) * exists(key) |}
                                // Already exists
                                present = true;
                                done = true;
                            {| fresh(e, key) * hasLock(pred) * hasLock(curr) * if (done) { exists(key) } else { emp } |}
                        } else {
                            {| fresh(e, key) * isEntry(curr) * lockedAndValidated(pred, curr)
                               * less(pred, e) * less(e, curr)
                            |}
                                <%{ #1.next := #2;
                                    #1.marked := false;
                                    #1.fresh := false;
                                    #3.next := #1 }(e, curr, pred)[]>;
                            {| hasLock(pred) * hasLock(curr) * exists(key) |}
                                present = false;
                                done = true;
                            {| hasLock(pred) * hasLock(curr) * if (done) { exists(key) } else { emp } |}
                        }
                    {| hasLock(pred) * hasLock(curr) * if (done) { exists(key) } else { emp } |}
                } else {
                    {| hasLock(pred) * hasLock(curr) |}
                        // We shouldn't need to do this...?
                        present = false;
                        done = false;
                    {| hasLock(pred) * hasLock(curr) * if (done) { exists(key) } else { emp } |}
                }
            {| hasLock(pred) * hasLock(curr) * if (done) { exists(key) } else { emp } |}
                <%{ releaseLock(#1) }(curr)[]>;
            {| hasLock(pred) * if (done) { exists(key) } else { emp } |}
                <%{ releaseLock(#1) }(pred)[]>;
            {| if (done) { exists(key) } else { emp } |}
        } while (!done);
    {| exists(key) |}
}

view fresh(Entry e, int key);
constraint fresh(e, key) ->
    lb < key && key < ub && %{#1 in X && #1.key == #2 && #1.fresh}(e, key);
constraint fresh(e1, k1) * fresh(e2, k2) -> e1 != e2;

view exists(int key);
constraint exists(key) -> %{ existsG(X, #1) }(key);

// The invariant is 'isSetG', which conjoins all the phi-predicates.
constraint emp -> %{ isSetG(X, #1, #2, #3, #4) }(head, tail, lb, ub);

// A key is wf if it is between the bounds.
constraint wf(key) -> lb < key && key < ub;

// This view encodes sigmaH.
constraint isHead(h) -> h == head;

// This view encodes sigmaT.
constraint isTail(t) -> t == tail;

constraint isEntry(e) -> %{ #1 in X && #1 != #2 && !#1.fresh }(e, tail);
constraint isEntryOrTail(e) -> %{ #1 in X && !#1.fresh }(e);

view nonfresh(Entry k);
constraint nonfresh(k) -> %{ !#1.fresh }(k);

// This view encodes sigmaK.
constraint keyIs(k, n) -> %{ #1 in X ==> #1.key == #2 }(n, k);

// This view encodes sigmaM.
constraint nonfresh(k) * isMarked(n) -> (k == n => %{ #1 in X ==> #1.marked }(n));

// This view encodes sigmaE.
constraint isExterior(n) -> %{ sigmaE(X, #1, #2, #3) }(head, tail, n);

constraint less(x, y) -> %{ #1.key < #2.key }(x, y);

constraint removed(k) -> %{ removedG(X, #1) }(k);

constraint hasLock(a)       ->  %{ #1 in X && entryLocked(#1) }(a);
constraint lockedAndValidated(a, b) -> %{ #1 in X && #2 in X && lockedAndValidatedG(#3, #1, #2) }(a, b, head);


constraint lockedAndValidated(a, b) * isEntry(c) -> (b == c) => %{ #1.key < #2.next.key }(a, b);

constraint hasLock(a)      * hasLock(b)       ->  a != b;
constraint hasLock(a)      * lockedAndValidated(b, c)   ->  a != b && a != c;
constraint lockedAndValidated(a, b)  * lockedAndValidated(c, d)   ->  a != c && a != d && b != c && b != d;
